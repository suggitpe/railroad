import java.text.SimpleDateFormat

buildscript {
    apply from: "$rootDir/dependencies.gradle"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath gradlePlugins.kotlin,
                gradlePlugins.springboot
    }
}

static def buildTimeStamp() {
    return new SimpleDateFormat("yyyyMMdd-HHmmss").format(new Date())
}

allprojects {
    defaultTasks 'clean', 'build'
}

subprojects { projects ->

    apply from: "$rootDir/dependencies.gradle"
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'

    group = 'org.suggs.sandbox.railroad'

    repositories {
        mavenCentral()
    }

    jar {
        archiveVersion = "${buildTimeStamp()}"
    }

    dependencies {
        implementation libs.kotlin,
                libs.kotlinReflect,
                libs.slf4j

        testImplementation libs.test.junitApi,
                libs.test.kotestAssertions,
                libs.test.mockito

        testRuntimeOnly libs.test.junitEngine,
                libs.test.junitPlatform,
                libs.logback
    }

    ext {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    test {
        useJUnitPlatform()
    }

}
